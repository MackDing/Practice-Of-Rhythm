# 汇编中的二进制求幂

汇编中二进制求幂的示例

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract AssemblyBinExp {
    // 二进制求幂来计算x**n
    function rpow(uint256 x, uint256 n, uint256 b)
        public
        pure
        returns (uint256 z)
    {
        assembly {
            switch x
            // x = 0
            case 0 {
                switch n
                // n = 0 --> x**n = 0**0 --> 1
                case 0 { z := b }
                // n > 0 --> x**n = 0**n --> 0
                default { z := 0 }
            }
            default {
                switch mod(n, 2)
                // x > 0 且 n 为偶数 --> z = 1
                case 0 { z := b }
                // x > 0 且 n 为奇数 --> z = x
                default { z := x }

                let half := div(b, 2) // 取整
                // n = n / 2, while n > 0, n = n / 2
                for { n := div(n, 2) } n { n := div(n, 2) } {
                    let xx := mul(x, x)
                    // 检查溢出 - 如果 xx / x != x 则恢复
                    if iszero(eq(div(xx, x), x)) { revert(0, 0) }
                    // Round (xx + half) / b
                    let xxRound := add(xx, half)
                    // 检查溢出 - 如果 xxRound < xx 则恢复
                    if lt(xxRound, xx) { revert(0, 0) }
                    x := div(xxRound, b)
                    // 如果 n % 2 == 1
                    if mod(n, 2) {
                        let zx := mul(z, x)
                        // 如果 x != 0 且 zx / x != z 则恢复
                        if and(iszero(iszero(x)), iszero(eq(div(zx, x), z))) {
                            revert(0, 0)
                        }
                        // 取整 (zx + half) / b
                        let zxRound := add(zx, half)
                        // 检查溢出 - 如果 zxRound < zx 则恢复
                        if lt(zxRound, zx) { revert(0, 0) }
                        z := div(zxRound, b)
                    }
                }
            }
        }
    }
}
```